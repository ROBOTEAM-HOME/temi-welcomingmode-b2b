apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.robotemi.welcomingbtob"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName '1.0.0'
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix "-DEBUG"
            testCoverageEnabled false
            debuggable true
            minifyEnabled false
            shrinkResources false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-rules-test.pro'
        }

        release {
            signingConfig signingConfigs.debug
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file("../secrets/platformLuckyStar.keystore")
            Properties properties = new Properties()
            FileInputStream fileInputStream = new FileInputStream("$project.rootDir/secrets/secret.properties")
            properties.load(fileInputStream)
            storePassword properties.getProperty("StorePassword")
            keyAlias properties.getProperty("KeyAlias")
            keyPassword properties.getProperty("KeyPassword")
            fileInputStream.close()
        }
    }

    flavorDimensions "country"

    productFlavors {
        usa {
            versionNameSuffix "-USA"
            applicationIdSuffix ".usa"
            dimension "country"
        }
        china {
            versionNameSuffix "-CHINA"
            applicationIdSuffix ".china"
            dimension "country"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    // Koin for Android
    implementation 'org.koin:koin-android:2.0.1'
    // or Koin for Lifecycle scoping
    implementation 'org.koin:koin-androidx-scope:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.hannesdorfmann.mosby:mvp:2.0.1'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    chinaImplementation files('src/sdk-china-debug.aar')
    usaImplementation files('src/sdk-usa-debug.aar')
}
